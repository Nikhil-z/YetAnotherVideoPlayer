apply plugin: 'com.android.application'

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.2"
    defaultConfig {
        //app package name
        applicationId "de.shadow578.yetanothervideoplayer"

        //version requirement and target of yavp
        //min:    6 Marshmallow
        //target: 10
        minSdkVersion 23
        targetSdkVersion 29

        // versioning using gitVersioner. If you want to work without git, change this
        // to a hardcoded string (or other versioning) + remove gitVersioner from the other .gradle files
        versionCode gitVersioner.versionCode
        versionName gitVersioner.versionName

        // info for automatic app updates.
        // needs to be present even if ENABLE_SELF_UPDATE is false, tho in that case the values may be empty
        // Fill in your github username and repo as you find it in the github url to the repo (eg. github.com/shadow578/YetAnotherVideoPlayer).
        // Updates are designed to use github releases, so if you want to use something else, you have to reimplement
        // the update functionality in feature/update/AppUpdateManager.
        // also, the updater expects the tags in github releases to be formatted as "v<version_int>", but you can change that with the regex below
        buildConfigField "String", "UPDATE_VENDOR", '"shadow578"'
        buildConfigField "String", "UPDATE_REPO", '"YetAnotherVideoPlayer"'
        buildConfigField "String", "UPDATE_VERSION_FORMAT", '"^v(\\\\d{3,})$"'

        //fixes vector drawables randomly changing color (idk why ¯\_(ツ)_/¯)
        vectorDrawables.useSupportLibrary = true

        //add version name to apk file name
        setProperty("archivesBaseName", "yavp-$versionName")
    }
    flavorDimensions "releasePlatform"
    productFlavors {
        github {
            //enable automatic update checks on builds for github
            buildConfigField "Boolean", "ENABLE_SELF_UPDATE", "true"
        }
        fdroid {
            //no auto- update checks for fdroid (this is part of their inclusion policy)
            isDefault true
            buildConfigField "Boolean", "ENABLE_SELF_UPDATE", "false"
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        targetCompatibility JavaVersion.VERSION_1_8
    }
    tasks.withType(JavaCompile) {
        configure(options) {
            options.encoding = 'UTF-8'
            options.debug = true
            options.failOnError = true
            options.warnings = true
            options.compilerArgs << '-Xlint:deprecation'
        }
    }
}

dependencies {
    //androidX
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'androidx.preference:preference:1.1.1'

    //tests
    //testImplementation 'junit:junit:4.12'
    //androidTestImplementation 'androidx.test:runner:1.2.0'
    //androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'

    //ui
    implementation 'com.google.android.material:material:1.1.0'
    implementation 'com.pnikosis:materialish-progress:1.7'
    implementation 'mobi.upod:time-duration-picker:1.1.3'

    //exoplayer
    implementation 'com.google.android.exoplayer:exoplayer:2.10.4'
    implementation 'com.github.MasayukiSuda:ExoPlayerFilter:v0.2.4'

    //http (currently only 4anime fix)
    compile 'com.squareup.okhttp:okhttp:2.5.0'
}
